import java.time.Instant

plugins {
    id 'application'
    id 'com.github.ben-manes.versions' version "$dependencyUpdatesPluginVersion"
    id 'net.researchgate.release' version "$releasePluginVersion"
    id 'jacoco'
}

group = 'com.ruchij.api'

repositories {
    mavenCentral()
}

dependencies {
    implementation(
            platform("org.mongodb:mongodb-driver-bom:$mongoDriverVersion"),
            "io.javalin:javalin-bundle:$javalinVersion",
            "com.typesafe:config:$typesafeConfigVersion",
            "ch.qos.logback:logback-classic:$logbackVersion",
            'org.mongodb:mongodb-driver-sync',
            'com.fasterxml.jackson.datatype:jackson-datatype-jdk8',
            "org.mindrot:jbcrypt:$jbcryptVersion",
            "com.github.javafaker:javafaker:$fakerVersion"

    )

    testImplementation(
            platform("org.junit:junit-bom:$junitVersion"),
            'org.junit.jupiter:junit-jupiter',
            'org.junit.platform:junit-platform-launcher',
            "org.mockito:mockito-core:$mockitoVersion",
            "org.testcontainers:junit-jupiter:$testcontainersVersion",
            "org.testcontainers:mongodb:$testcontainersVersion"
    )
}

application {
    mainClass = 'com.ruchij.api.ApiApp'
}

tasks.register('buildInformation') {
    doLast {
        def file = new File("${getLayout().getProjectDirectory()}/build/resources/main/build-information.json")
        file.parentFile.mkdirs()

        if (file.exists()) {
            file.delete()
        }

        def buildInfo =
                """{
                "name": "tic-tac-toe-backend",
                "group": "${project.group}",
                "version": "${project.version}",
                "gradleVersion": "${gradle.gradleVersion}",
                "buildTimestamp": "${Instant.now()}",
                "gitBranch": "${'git rev-parse --abbrev-ref HEAD'.execute().text.trim()}",
                "gitCommit": "${'git rev-parse --short HEAD'.execute().text.trim()}"
            }"""

        file.withWriter('UTF-8') { writer ->
            writer.write(buildInfo)
        }
    }
}

processResources {
    dependsOn buildInformation
}

release {
    preTagCommitMessage = 'Setting release version: '
    newVersionCommitMessage = 'Setting development version: '
    pushReleaseVersionBranch = 'main'

    git {
        requireBranch.set('dev')
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

distTar {
    archiveFileName = 'api.tar'
}

jacocoTestReport {
    dependsOn test
}
